Window.SetBackgroundTopColor(0.3,0.3,0.8);
Window.SetBackgroundBottomColor(0,0,0.7);

# Asignacion de imagenes a un hash
fun Asignacion_Hash (N_Sprites, Nombre_Base, Nombre_Complemento)
{
    for (i=0; i < N_Sprites; ++i)
	 New_Hash[i] = Image(Nombre_Base +
    	      		     Nombre_Complemento +
			     i + ".png");

    return New_Hash;
}


# Numero de Sprites
Nyan.Sprites = 6;

# Nombre base de los sprites
Nyan.NombreBase = "nyancat";

# Identificador de el personaje en los sprites
Nyan.NombrePersonaje = "simple";
#Nyan.NombrePersonaje = "ninja";

Nyan.Avanze = 0.3;
# Creacion de un objeto sprite - Necesario para mostrar en pantalla
Nyan.Sprite = Sprite();


Nyan.Hash = Asignacion_Hash(Nyan.Sprites,
	    		    Nyan.NombreBase + "-",
			    Nyan.NombrePersonaje + "-");

# Cordenadas X,Y - Centro de pantalla.
Nyan.X = (Window.GetWidth() / 2) - (Nyan.Hash[0].GetWidth() / 2);
Nyan.Y = (Window.GetHeight() / 2) - (Nyan.Hash[0].GetHeight() / 2);

indice = 0;

# ------------------------- < Arcoiris > ---------------------------------------

Arcoiris.NumSprites = 0;
Arcoiris.X = 0;
Arcoiris.Y = Nyan.Y + (Nyan.Hash[0].GetHeight() / 8);
Arcoiris.VelocidadAvanze = 0.5;
Arcoiris.Avanze = 0;
Arcoiris.NumImagenes = 4;
PosicionX = Nyan.X + (Nyan.Hash[0].GetWidth() / 4);
ScaleY = Nyan.Hash[0].GetHeight() / 1.5;
contador = 0;

Arcoiris.Imagenes = Asignacion_Hash(Arcoiris.NumImagenes, "arcoiris-","solo-");

for (i=0; i < Arcoiris.NumImagenes; i++)
    Arcoiris.Imagenes[i] = Arcoiris.Imagenes[i].Scale( Arcoiris.Imagenes[i].GetWidth(), ScaleY);

while (Arcoiris.X < PosicionX)
{
    Arcoiris.HashSprites[Arcoiris.NumSprites] = Sprite(Arcoiris.Imagenes[contador]);
    if (contador == 3 || contador == 1)
	Arcoiris.HashSprites[Arcoiris.NumSprites].SetPosition(Arcoiris.X,
							  Arcoiris.Y,
							  -3);

    else
    Arcoiris.HashSprites[Arcoiris.NumSprites].SetPosition(Arcoiris.X,
							  Arcoiris.Y + (10 * Math.Sin(contador - 1)),
							  -3);

    Arcoiris.NumSprites++;
    Arcoiris.X +=  Arcoiris.Imagenes[contador].GetWidth();

    contador++;
    if (contador >= Arcoiris.NumImagenes)
	contador = 0;

}

# ------------------------- </ arcoiris > --------------------------------------

# Funcion de animacion
fun refresh ()
{
    if (indice > Nyan.Sprites)
	indice = 0;
    else
	indice += Nyan.Avanze;

    # Especificacion de imagen de acuerdo al indice
    Nyan.Sprite.SetImage(Nyan.Hash[Math.Int(indice)]);
    # Hubicacion de imagen en pantalla
    Nyan.Sprite.SetX (Nyan.X);
    Nyan.Sprite.SetY (Nyan.Y);

    Sprites = 0;

    if (Arcoiris.Avanze > Nyan.Sprites)
       Arcoiris.Avanze = 0;

       Arcoiris.Avanze +=  Arcoiris.VelocidadAvanze;

    while (Sprites < Arcoiris.NumSprites && Arcoiris.Avanze == Nyan.Sprites )
     {
	 if (Arcoiris.HashSprites[Sprites].GetY() < Arcoiris.Y)
	 {
		 Arcoiris.HashSprites[Sprites].SetImage(Arcoiris.Imagenes[0]);
	   	 Arcoiris.HashSprites[Sprites].SetY(Arcoiris.Y + 10);
	}
	 else if (Arcoiris.HashSprites[Sprites].GetY() > Arcoiris.Y)
	 {
		 Arcoiris.HashSprites[Sprites].SetImage(Arcoiris.Imagenes[2]);
		 Arcoiris.HashSprites[Sprites].SetY(Arcoiris.Y - 5);
	}

 	Sprites+=2;
    }

}

Plymouth.SetRefreshFunction(refresh);